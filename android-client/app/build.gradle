apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId 'edu.cmu.cs.wca_glass_thumbsup'
        minSdkVersion 27
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            buildConfigField("String", "GABRIEL_HOST", properties.getProperty('gabrielHost'))
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.8.0'
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option "lite"
                    }
                    python {
                        // This gradle file is located in <REPOSITORY ROOT>/android-client/app
                        // outputSubDir is relative to <REPOSITORY ROOT>/android-client/app/build/generated/source/proto/debug
                        outputSubDir = '../../../../../../../server'
                    }
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.core:core:1.9.0'
    implementation 'com.google.android.material:material:1.6.0'

    implementation 'edu.cmu.cs.gabriel:client:2.4.1'
    implementation 'edu.cmu.cs.gabriel:protocol:2.0.1'
    implementation 'edu.cmu.cs.gabriel:camera:2.4.1'

    def camerax_version = "1.0.0-rc03"
    // CameraX core library using camera2 implementation
    //noinspection GradleDependency
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha22"
    // Zoom Meeting SDK
    implementation project(':mobilertc')

    // MediaPipe Hands Solution
    implementation 'com.google.mediapipe:solution-core:latest.release'
    implementation 'com.google.mediapipe:hands:latest.release'
}